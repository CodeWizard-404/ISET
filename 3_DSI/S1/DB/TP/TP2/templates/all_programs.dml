
DEFINE PRODUCT_ALERT PROGRAM
PROGRAM
vrb _alert   text
vrb _product number

trap on error

_product = product + 0

tempstat product
do
  limit product to children using product_parentrel _product
  limit product keep SALES_CUBE_SALES_PY_PCT_CHG lt 0

report down product SALES_CUBE_SALES_PY_PCT_CHG

  if statlen(product) gt 0
    then _alert = 'ALERT'
    else _alert = 'OKAY'

doend

return _alert

error:
return 'ERROR'
END



DEFINE FORECAST_CUSTOM_AGGMAP AGGMAP
AGGMAP
RELATION this_aw!TIME_PARENTREL PRECOMPUTE(ALL) OPERATOR SUM args  DIVIDEBYZERO YES DECIMALOVERFLOW YES NASKIP YES COUNT NO
RELATION this_aw!GEOGRAPHY_PARENTREL PRECOMPUTE(ALL) OPERATOR SUM args  DIVIDEBYZERO YES DECIMALOVERFLOW YES NASKIP YES COUNT NO
RELATION this_aw!PRODUCT_PARENTREL PRECOMPUTE(ALL) OPERATOR SUM args  DIVIDEBYZERO YES DECIMALOVERFLOW YES NASKIP YES COUNT NO
AGGINDEX NO
CACHE SESSION
END

DEFINE DO_FORECAST PROGRAM
PROGRAM
vrb _handle    int

trap on error

"Save the state of the dimensions
pushlevel 'do_forecast'
push geography product time channel

"LIMIT commands scope the data the will be operated on
"The scope persists throughout the procedure
limit geography to geography_levelrel eq 'REGION'
limit product to product_levelrel eq 'TYPE'
limit channel to 'ALL_CHANNELS'
limit time to time_levelrel eq 'MONTH'
sort time a time_end_date


"Define variables used to help process the forecast function
if not exists('forecast_seasonal')
  then DEFINE FORECAST_SEASONAL VARIABLE NUMBER <TIME PRODUCT GEOGRAPHY>
if not exists('forecast_smseasonal')
  then DEFINE FORECAST_SMSEASONAL VARIABLE NUMBER <TIME PRODUCT GEOGRAPHY>


"Initialize forecasts
forecast_best_fit = NA
forecast_linear_regression = NA

"*******************************
"* Best-fit
"*******************************
_handle = FCOPEN('MyForecast')

"Set forecast parameters for 'best fit'
fcset _handle APPROACH 'APPAUTO' periodicity 12 histperiods 36

"Execute the forecast - save seasonal and seasonal smoothed into the variables just defined
FCEXEC _handle time time INTO forecast_best_fit -
       seasonal forecast_seasonal -
       smseasonal forecast_smseasonal backcast -
       sales_cube_sales

"Close the forecast
FCCLOSE _handle

"*******************************
"* Linear regression
"*******************************
_handle = FCOPEN('MyForecast')

"Set forecast parameters for 'double exponential smoothing'
fcset _handle APPROACH 'APPMANUAL' method 'LINREG' periodicity 12 histperiods 36

"Execute the forecast
FCEXEC _handle time time INTO forecast_linear_regression -
       backcast -
       sales_cube_sales

"Close the forecast
FCCLOSE _handle


"Aggregate the forecasts
AGGREGATE forecast_best_fit, forecast_linear_regression USING FORECAST_CUSTOM_AGGMAP

update
commit

poplevel 'do_forecast'

return

error:
poplevel 'do_forecast'
if _handle ne NA
then FCCLOSE _handle
END

DEFINE ASSIGN_COS PROGRAM
PROGRAM
trap on error

pushlevel 'assign_cos'
push product, geography, channel, time

limit product to product_levelrel eq 'ITEM'
limit channel to channel_levelrel eq 'CHANNEL'
limit time to time_levelrel eq 'MONTH'
limit geography to geography_levelrel eq 'STATE_PROVINCE'

sales_cube_stored(sales_cube_measure_dim 'COST_OF_SALES') = (cost_cube_unit_cost * sales_cube_stored(sales_cube_measure_dim 'QUANTITY')) across sales_cube_composite

poplevel 'assign_cos'
return

error:
show errortext
poplevel 'assign_cos'
return
END
